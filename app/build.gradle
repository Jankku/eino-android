plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'androidx.navigation.safeargs.kotlin'
    id 'dagger.hilt.android.plugin'
    id 'kotlin-kapt'
}

def baseUrlFile = rootProject.file("baseurl.properties")
def baseUrlProperties = new Properties()
baseUrlProperties.load(new FileInputStream(baseUrlFile))

def signingFile = rootProject.file("signing.properties")
def signingProperties = new Properties()
signingProperties.load(new FileInputStream(signingFile))

android {
    compileSdk 33
    defaultConfig {
        applicationId "com.jankku.eino"
        minSdk 24
        targetSdk 33
        versionCode 9
        versionName "1.1"
        buildConfigField "String", "baseUrl", baseUrlProperties['baseUrl']
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        release {
            try {
                storeFile file(signingProperties['storeFile'])
                storePassword signingProperties['storePassword']
                keyAlias signingProperties['keyAlias']
                keyPassword signingProperties['keyPassword']
            } catch (ignored) {
                throw new InvalidPropertiesFormatException("Define 'storeFile', 'storePassword', 'keyAlias' and 'keyPassword' in signing.properties.")
            }
        }
    }
    buildFeatures {
        viewBinding true
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            versionNameSuffix "-debug"
            applicationIdSuffix ".debug"
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled = true
    }
    namespace 'com.jankku.eino'
}

dependencies {
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    coreLibraryDesugaring("com.android.tools:desugar_jdk_libs:2.0.3")

    def appcompat_version = "1.6.1"
    implementation "androidx.appcompat:appcompat:$appcompat_version"
    implementation "androidx.appcompat:appcompat-resources:$appcompat_version"

    implementation "androidx.core:core-ktx:1.10.0"
    implementation "androidx.constraintlayout:constraintlayout:2.1.4"
    implementation "androidx.activity:activity-ktx:1.7.1"
    implementation "androidx.fragment:fragment-ktx:1.5.7"
    implementation "com.google.android.material:material:1.8.0"
    implementation "androidx.recyclerview:recyclerview:1.3.0"
    implementation "androidx.preference:preference-ktx:1.2.0"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
    implementation "androidx.core:core-splashscreen:1.0.1"

    implementation "androidx.datastore:datastore:1.0.0"
    implementation "androidx.datastore:datastore-preferences:1.0.0"

    implementation "androidx.navigation:navigation-runtime-ktx:$nav_version"
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    def retrofit_version = "2.9.0"
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit_version"
    implementation "com.squareup.okhttp3:logging-interceptor:4.9.2"

    def moshi_version = "1.14.0"
    implementation "com.squareup.moshi:moshi-kotlin:$moshi_version"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$moshi_version"

    def coroutines_version = "1.6.4"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    def lifecycle_version = "2.6.1"
    implementation "androidx.lifecycle:lifecycle-common:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"

    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"
    implementation "androidx.hilt:hilt-navigation-fragment:1.0.0"
    kapt "androidx.hilt:hilt-compiler:1.0.0"

    implementation 'com.github.bumptech.glide:glide:4.14.2'
    implementation 'com.basgeekball:awesome-validation:4.3'
    implementation 'com.auth0.android:jwtdecode:2.0.1'
    implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0'

    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.10'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}